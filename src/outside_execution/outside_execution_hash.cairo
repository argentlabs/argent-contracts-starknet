use argent::offchain_message::{
    IOffChainMessageHashRev0, IOffChainMessageHashRev1, IStructHashRev0, IStructHashRev1, StarkNetDomain,
    StarknetDomain, StructHashStarkNetDomain, get_message_hash_rev_1_with_precalc,
};
use argent::outside_execution::outside_execution::OutsideExecution;
use core::hash::{HashStateExTrait, HashStateTrait};
use core::pedersen::PedersenTrait;
use core::poseidon::poseidon_hash_span;
use starknet::{account::Call, get_contract_address, get_tx_info};

pub const MAINNET_FIRST_HADES_PERMUTATION: (felt252, felt252, felt252) = (
    2727651893633223888261849279042022325174182119102281398572272198960815727249,
    729016093840936084580216898033636860729342953928695140840860652272753125883,
    2792630223211151632174198306610141883878913626231408099903852589995722964080,
);

pub const SEPOLIA_FIRST_HADES_PERMUTATION: (felt252, felt252, felt252) = (
    3580606761507954093996364807837346681513890124685758374532511352257317798951,
    3431227198346789440159663709265467470274870120429209591243179659934705045436,
    974062396530052497724701732977002885691473732259823426261944148730229556466,
);


const OUTSIDE_CALL_TYPE_HASH_REV_0: felt252 = selector!(
    "OutsideCall(to:felt,selector:felt,calldata_len:felt,calldata:felt*)",
);

const OUTSIDE_EXECUTION_TYPE_HASH_REV_0: felt252 = selector!(
    "OutsideExecution(caller:felt,nonce:felt,execute_after:felt,execute_before:felt,calls_len:felt,calls:OutsideCall*)OutsideCall(to:felt,selector:felt,calldata_len:felt,calldata:felt*)",
);

const OUTSIDE_EXECUTION_TYPE_HASH_REV_1: felt252 = selector!(
    "\"OutsideExecution\"(\"Caller\":\"ContractAddress\",\"Nonce\":\"felt\",\"Execute After\":\"u128\",\"Execute Before\":\"u128\",\"Calls\":\"Call*\")\"Call\"(\"To\":\"ContractAddress\",\"Selector\":\"selector\",\"Calldata\":\"felt*\")",
);

const CALL_TYPE_HASH_REV_1: felt252 = selector!(
    "\"Call\"(\"To\":\"ContractAddress\",\"Selector\":\"selector\",\"Calldata\":\"felt*\")",
);

impl StructHashOutsideExecutionRev0 of IStructHashRev0<OutsideExecution> {
    fn get_struct_hash_rev_0(self: @OutsideExecution) -> felt252 {
        let self = *self;
        let mut state = PedersenTrait::new(0);
        let mut calls_span = self.calls;
        let calls_len = self.calls.len().into();
        for call in calls_span {
            state = state.update((call.get_struct_hash_rev_0()));
        };
        let calls_hash = state.update(calls_len).finalize();

        PedersenTrait::new(0)
            .update_with(OUTSIDE_EXECUTION_TYPE_HASH_REV_0)
            .update_with(self.caller)
            .update_with(self.nonce)
            .update_with(self.execute_after)
            .update_with(self.execute_before)
            .update_with(calls_len)
            .update_with(calls_hash)
            .update_with(7)
            .finalize()
    }
}

impl StructHashCallRev0 of IStructHashRev0<Call> {
    fn get_struct_hash_rev_0(self: @Call) -> felt252 {
        let mut state = PedersenTrait::new(0);
        let mut calldata_span = *self.calldata;
        let calldata_len = calldata_span.len().into();
        for item in calldata_span {
            state = state.update(*item)
        };
        let calldata_hash = state.update(calldata_len).finalize();

        PedersenTrait::new(0)
            .update_with(OUTSIDE_CALL_TYPE_HASH_REV_0)
            .update_with(*self.to)
            .update_with(*self.selector)
            .update_with(calldata_len)
            .update_with(calldata_hash)
            .update_with(5)
            .finalize()
    }
}

pub impl OffChainMessageOutsideExecutionRev0 of IOffChainMessageHashRev0<OutsideExecution> {
    fn get_message_hash_rev_0(self: @OutsideExecution) -> felt252 {
        let domain = StarkNetDomain {
            name: 'Account.execute_from_outside', version: 1, chain_id: get_tx_info().chain_id,
        };

        PedersenTrait::new(0)
            .update_with('StarkNet Message')
            .update_with(domain.get_struct_hash_rev_0())
            .update_with(get_contract_address())
            .update_with((*self).get_struct_hash_rev_0())
            .update(4)
            .finalize()
    }
}

impl StructHashCallRev1 of IStructHashRev1<Call> {
    fn get_struct_hash_rev_1(self: @Call) -> felt252 {
        poseidon_hash_span(
            array![CALL_TYPE_HASH_REV_1, (*self.to).into(), *self.selector, poseidon_hash_span(*self.calldata)].span(),
        )
    }
}

impl StructHashOutsideExecutionRev1 of IStructHashRev1<OutsideExecution> {
    fn get_struct_hash_rev_1(self: @OutsideExecution) -> felt252 {
        let self = *self;
        let mut hashed_calls = array![];
        for call in self.calls {
            hashed_calls.append(call.get_struct_hash_rev_1());
        };
        poseidon_hash_span(
            array![
                OUTSIDE_EXECUTION_TYPE_HASH_REV_1,
                self.caller.into(),
                self.nonce,
                self.execute_after.into(),
                self.execute_before.into(),
                poseidon_hash_span(hashed_calls.span()),
            ]
                .span(),
        )
    }
}

pub impl OffChainMessageOutsideExecutionRev1 of IOffChainMessageHashRev1<OutsideExecution> {
    fn get_message_hash_rev_1(self: @OutsideExecution) -> felt252 {
        // Version is a felt instead of a shortstring in SNIP-9 due to a mistake in the Braavos contracts and has been
        // copied for compatibility.
        // Revision will also be a felt instead of a shortstring for all SNIP12-rev1 signatures because of the same
        // issue

        let chain_id = get_tx_info().chain_id;
        if chain_id == 'SN_MAIN' {
            return get_message_hash_rev_1_with_precalc(MAINNET_FIRST_HADES_PERMUTATION, *self);
        }
        if chain_id == 'SN_SEPOLIA' {
            return get_message_hash_rev_1_with_precalc(SEPOLIA_FIRST_HADES_PERMUTATION, *self);
        }
        let domain = StarknetDomain { name: 'Account.execute_from_outside', version: 2, chain_id, revision: 1 };
        poseidon_hash_span(
            array![
                'StarkNet Message',
                domain.get_struct_hash_rev_1(),
                get_contract_address().into(),
                (*self).get_struct_hash_rev_1(),
            ]
                .span(),
        )
    }
}

